/*
 * SPDX-FileCopyrightText: Copyright (c) 2025-2026, NVIDIA CORPORATION.
 * SPDX-License-Identifier: Apache-2.0
 */

#pragma once

#include "../../../core/nvtx.hpp"
#include "../../../neighbors/detail/vpq_dataset.cuh"
#include "pq_codepacking.cuh"  // pq_bits-bitfield

#include <cuvs/cluster/kmeans.hpp>
#include <cuvs/preprocessing/quantize/pq.hpp>
#include <raft/core/operators.hpp>
#include <raft/matrix/init.cuh>

#include "../../../cluster/kmeans_balanced.cuh"

namespace cuvs::preprocessing::quantize::pq::detail {

inline void fill_missing_params_heuristics(cuvs::preprocessing::quantize::pq::params& params,
                                           size_t n_rows,
                                           size_t dim)
{
  if (params.pq_dim == 0) { params.pq_dim = raft::div_rounding_up_safe(dim, size_t{4}); }
  if (params.vq_n_centers == 0) {
    params.vq_n_centers = raft::round_up_safe<uint32_t>(std::sqrt(n_rows), 8);
  }
}

inline auto to_vpq_params(const cuvs::preprocessing::quantize::pq::params& params)
  -> cuvs::neighbors::vpq_params
{
  return cuvs::neighbors::vpq_params{
    .pq_bits                         = params.pq_bits,
    .pq_dim                          = params.pq_dim,
    .vq_n_centers                    = params.vq_n_centers,
    .kmeans_n_iters                  = params.kmeans_n_iters,
    .vq_kmeans_trainset_fraction     = 1.0,
    .pq_kmeans_trainset_fraction     = 1.0,
    .pq_kmeans_type                  = params.pq_kmeans_type,
    .max_train_points_per_pq_code    = params.max_train_points_per_pq_code,
    .max_train_points_per_vq_cluster = params.max_train_points_per_vq_cluster};
}

template <typename MathT, typename DatasetT>
auto train_pq_subspaces(
  const raft::resources& res,
  const cuvs::preprocessing::quantize::pq::params& params,
  const DatasetT& dataset,
  const raft::device_matrix_view<const MathT, uint32_t, raft::row_major> vq_centers)
  -> raft::device_matrix<MathT, uint32_t, raft::row_major>
{
  using ix_t              = int64_t;
  const ix_t n_rows       = dataset.extent(0);
  const ix_t dim          = dataset.extent(1);
  const ix_t pq_dim       = params.pq_dim;
  const ix_t pq_bits      = params.pq_bits;
  const ix_t pq_n_centers = ix_t{1} << pq_bits;
  const ix_t pq_len       = raft::div_rounding_up_safe(dim, pq_dim);
  RAFT_EXPECTS((dim % pq_dim) == 0, "Dimension must be divisible by pq_dim");
  const ix_t n_rows_train =
    std::min<ix_t>(n_rows, params.max_train_points_per_pq_code * pq_n_centers);
  RAFT_EXPECTS(
    n_rows_train >= pq_n_centers,
    "The number of training samples must be equal to or greater than the number of PQ centers");
  auto pq_trainset = raft::make_device_matrix<MathT, ix_t, raft::row_major>(res, 0, 0);
  // Subtract VQ centers
  if (!vq_centers.empty()) {
    pq_trainset    = cuvs::util::subsample(res, dataset, n_rows_train);
    auto vq_labels = raft::make_device_vector<uint32_t, ix_t>(res, pq_trainset.extent(0));
    cuvs::neighbors::detail::predict_vq<uint32_t>(
      res, raft::make_const_mdspan(pq_trainset.view()), vq_centers, vq_labels.view());
    raft::linalg::map_offset(
      res,
      pq_trainset.view(),
      [labels = vq_labels.view(), vq_centers, dim] __device__(ix_t off, MathT x) {
        ix_t i = off / dim;
        ix_t j = off % dim;
        return x - vq_centers(labels(i), j);
      },
      raft::make_const_mdspan(pq_trainset.view()));
  }

  // Train PQ centers for each subspace
  auto sub_dataset = raft::make_device_matrix<MathT, ix_t>(res, n_rows_train, pq_len);

  auto pq_centers =
    raft::make_device_matrix<MathT, uint32_t, raft::row_major>(res, pq_dim * pq_n_centers, pq_len);
  auto trainset_ptr     = !vq_centers.empty() ? pq_trainset.data_handle() : dataset.data_handle();
  auto sub_labels       = raft::make_device_vector<uint32_t, ix_t>(res, 0);
  auto pq_cluster_sizes = raft::make_device_vector<uint32_t, ix_t>(res, 0);
  auto device_memory    = raft::resource::get_workspace_resource(res);
  if (params.pq_kmeans_type == cuvs::cluster::kmeans::kmeans_type::KMeansBalanced) {
    sub_labels = raft::make_device_mdarray<uint32_t>(
      res, device_memory, raft::make_extents<ix_t>(n_rows_train));
    pq_cluster_sizes = raft::make_device_mdarray<uint32_t>(
      res, device_memory, raft::make_extents<ix_t>(pq_n_centers));
  }

  for (ix_t m = 0; m < pq_dim; m++) {
    RAFT_CUDA_TRY(cudaMemcpy2DAsync(sub_dataset.data_handle(),
                                    sizeof(MathT) * pq_len,
                                    trainset_ptr + m * pq_len,
                                    sizeof(MathT) * dim,
                                    sizeof(MathT) * pq_len,
                                    n_rows_train,
                                    cudaMemcpyDefault,
                                    raft::resource::get_cuda_stream(res)));
    auto pq_centers_subspace_view = raft::make_device_matrix_view<MathT, uint32_t, raft::row_major>(
      pq_centers.data_handle() + m * pq_n_centers * pq_len, pq_n_centers, pq_len);
    cuvs::neighbors::detail::train_pq_centers<MathT, ix_t>(
      res,
      to_vpq_params(params),
      raft::make_const_mdspan(sub_dataset.view()),
      pq_centers_subspace_view,
      sub_labels.view(),
      pq_cluster_sizes.view());
  }
  return pq_centers;
}

template <typename DataT, typename MathT, typename AccessorType>
quantizer<MathT> build(
  raft::resources const& res,
  const cuvs::preprocessing::quantize::pq::params params,
  raft::mdspan<const DataT, raft::matrix_extent<int64_t>, raft::row_major, AccessorType> dataset)
{
  auto n_rows = dataset.extent(0);
  auto dim    = dataset.extent(1);
  raft::common::nvtx::range<cuvs::common::nvtx::domain::cuvs> fun_scope(
    "preprocessing::quantize::pq::build(%zu, %u, %u, %u)",
    size_t(n_rows),
    dim,
    params.pq_bits,
    params.pq_dim);
  RAFT_EXPECTS(params.pq_bits >= 4 && params.pq_bits <= 16,
               "PQ bits must be within [4, 16], got %u",
               params.pq_bits);

  auto filled_params = params;
  fill_missing_params_heuristics(filled_params, n_rows, dim);
  auto vpq_params   = to_vpq_params(filled_params);
  auto vq_code_book = raft::make_device_matrix<MathT, uint32_t, raft::row_major>(res, 0, 0);
  if (filled_params.use_vq) {
    vq_code_book = cuvs::neighbors::detail::train_vq<MathT>(res, vpq_params, dataset);
  }
  auto empty_codes  = raft::make_device_matrix<uint8_t, int64_t, raft::row_major>(res, 0, 0);
  auto pq_code_book = raft::make_device_matrix<MathT, uint32_t, raft::row_major>(res, 0, 0);
  if (filled_params.use_subspaces) {
    pq_code_book = train_pq_subspaces<MathT>(
      res, filled_params, dataset, raft::make_const_mdspan(vq_code_book.view()));
  } else {
    pq_code_book = cuvs::neighbors::detail::train_pq<MathT>(
      res, vpq_params, dataset, raft::make_const_mdspan(vq_code_book.view()));
  }
  return {filled_params,
          cuvs::neighbors::vpq_dataset<MathT, int64_t>{
            std::move(vq_code_book), std::move(pq_code_book), std::move(empty_codes)}};
}

template <typename T, typename QuantI, typename AccessorType>
void transform(
  raft::resources const& res,
  const quantizer<T>& quantizer,
  raft::mdspan<const T, raft::matrix_extent<int64_t>, raft::row_major, AccessorType> dataset,
  raft::device_matrix_view<QuantI, int64_t> pq_codes_out,
  std::optional<raft::device_vector_view<uint32_t, int64_t>> vq_labels = std::nullopt)
{
  raft::common::nvtx::range<cuvs::common::nvtx::domain::cuvs> fun_scope(
    "preprocessing::quantize::pq::transform(%zu, %zu, %zu)",
    size_t(dataset.extent(0)),
    size_t(dataset.extent(1)),
    size_t(pq_codes_out.extent(1)));
  RAFT_EXPECTS(pq_codes_out.extent(0) == dataset.extent(0),
               "Output matrix must have the same number of rows as the input dataset");
  RAFT_EXPECTS(pq_codes_out.extent(1) == get_quantized_dim(quantizer.params_quantizer),
               "Output matrix doesn't have the correct number of columns");
  RAFT_EXPECTS(quantizer.params_quantizer.pq_bits >= 4 && quantizer.params_quantizer.pq_bits <= 16,
               "PQ bits must be within [4, 16]");
  // Encode dataset
  auto vq_centers     = raft::make_const_mdspan(quantizer.vpq_codebooks.vq_code_book.view());
  auto vq_labels_view = raft::make_device_vector_view<uint32_t, int64_t>(nullptr, 0);
  if (vq_labels.has_value()) { vq_labels_view = vq_labels.value(); }

  if (quantizer.params_quantizer.use_subspaces) {
    cuvs::neighbors::detail::process_and_fill_codes_subspaces<T, int64_t>(
      res,
      to_vpq_params(quantizer.params_quantizer),
      dataset,
      raft::make_const_mdspan(quantizer.vpq_codebooks.pq_code_book.view()),
      vq_centers,
      vq_labels_view,
      pq_codes_out);
  } else {
    cuvs::neighbors::detail::process_and_fill_codes<T, int64_t>(
      res,
      to_vpq_params(quantizer.params_quantizer),
      dataset,
      raft::make_const_mdspan(quantizer.vpq_codebooks.pq_code_book.view()),
      vq_centers,
      vq_labels_view,
      pq_codes_out);
  }
}

template <uint32_t BlockSize,
          uint32_t SubWarpSize,
          typename CodeT,
          typename DataT,
          typename MathT,
          typename IdxT,
          typename LabelT>
__launch_bounds__(BlockSize) RAFT_KERNEL reconstruct_vectors_kernel(
  raft::device_matrix_view<const uint8_t, IdxT, raft::row_major> codes,
  raft::device_matrix_view<DataT, IdxT, raft::row_major> dataset,
  raft::device_matrix_view<const MathT, uint32_t, raft::row_major> pq_centers,
  raft::device_matrix_view<const DataT, uint32_t, raft::row_major> vq_centers,
  std::optional<raft::device_vector_view<const uint32_t, int64_t>> vq_labels,
  const uint32_t pq_bits,
  bool use_subspaces)
{
  const uint32_t n_centers = 1 << pq_bits;
  using subwarp_align      = raft::Pow2<SubWarpSize>;
  const IdxT row_ix = subwarp_align::div(IdxT{threadIdx.x} + IdxT{BlockSize} * IdxT{blockIdx.x});
  if (row_ix >= dataset.extent(0)) { return; }
  uint32_t lane_id      = subwarp_align::mod(raft::laneId());
  const uint32_t pq_len = raft::div_rounding_up_unsafe(dataset.extent(1), pq_centers.extent(1));

  LabelT vq_label = 0;
  if (vq_labels.has_value()) { vq_label = vq_labels.value()(row_ix); }
  cuvs::preprocessing::quantize::detail::bitfield_view_t code_view{&codes(row_ix, 0), pq_bits};
  for (uint32_t j = lane_id; j < pq_len; j += SubWarpSize) {
    const CodeT code = code_view[j];
    for (uint32_t k = 0; k < pq_centers.extent(1); k++) {
      const auto col                    = j * pq_centers.extent(1) + k;
      const uint32_t pq_subspace_offset = use_subspaces ? n_centers * j : 0;
      if (!vq_centers.empty()) {
        dataset(row_ix, col) = pq_centers(pq_subspace_offset + code, k) + vq_centers(vq_label, col);
      } else {
        dataset(row_ix, col) = pq_centers(pq_subspace_offset + code, k);
      }
    }
  }
}

template <typename DataT, typename MathT, typename IdxT, typename LabelT>
auto reconstruct_vectors(
  const raft::resources& res,
  const params& params,
  raft::device_matrix_view<const uint8_t, IdxT, raft::row_major> codes,
  raft::device_matrix_view<const MathT, uint32_t, raft::row_major> pq_centers,
  raft::device_matrix_view<const DataT, uint32_t, raft::row_major> vq_centers,
  std::optional<raft::device_vector_view<const uint32_t, int64_t>> vq_labels,
  raft::device_matrix_view<DataT, IdxT, raft::row_major> out_vectors,
  bool use_subspaces)
{
  const IdxT n_rows       = out_vectors.extent(0);
  const IdxT dim          = out_vectors.extent(1);
  const IdxT pq_dim       = params.pq_dim;
  const IdxT pq_bits      = params.pq_bits;
  const IdxT pq_n_centers = IdxT{1} << pq_bits;

  auto stream = raft::resource::get_cuda_stream(res);

  constexpr IdxT kBlockSize  = 256;
  const IdxT threads_per_vec = std::min<IdxT>(raft::WarpSize, pq_n_centers);
  dim3 threads(kBlockSize, 1, 1);
  auto kernel = [](uint32_t pq_bits) {
    if (pq_bits == 4) {
      return reconstruct_vectors_kernel<kBlockSize, 16, uint8_t, DataT, MathT, IdxT, LabelT>;
    } else if (pq_bits <= 8) {
      return reconstruct_vectors_kernel<kBlockSize,
                                        raft::WarpSize,
                                        uint8_t,
                                        DataT,
                                        MathT,
                                        IdxT,
                                        LabelT>;
    } else if (pq_bits <= 16) {
      return reconstruct_vectors_kernel<kBlockSize,
                                        raft::WarpSize,
                                        uint16_t,
                                        DataT,
                                        MathT,
                                        IdxT,
                                        LabelT>;
    } else {
      RAFT_FAIL("Invalid pq_bits (%u), the value must be within [4, 16]", pq_bits);
    }
  }(pq_bits);
  dim3 blocks(raft::div_rounding_up_safe<IdxT>(n_rows, kBlockSize / threads_per_vec), 1, 1);
  kernel<<<blocks, threads, 0, stream>>>(
    codes, out_vectors, pq_centers, vq_centers, vq_labels, pq_bits, use_subspaces);
  RAFT_CUDA_TRY(cudaPeekAtLastError());

  return codes;
}

template <typename T, typename QuantI = uint8_t>
void inverse_transform(
  raft::resources const& res,
  const quantizer<T>& quant,
  raft::device_matrix_view<const QuantI, int64_t> codes,
  raft::device_matrix_view<T, int64_t> out,
  std::optional<raft::device_vector_view<const uint32_t, int64_t>> vq_labels = std::nullopt)
{
  raft::common::nvtx::range<cuvs::common::nvtx::domain::cuvs> fun_scope(
    "preprocessing::quantize::pq::inverse_transform(%zu, %zu, %zu)",
    size_t(codes.extent(0)),
    size_t(codes.extent(1)),
    size_t(out.extent(1)));
  using label_t = uint32_t;
  using idx_t   = int64_t;
  RAFT_EXPECTS(out.extent(0) == codes.extent(0),
               "Output matrix must have the same number of rows as the input codes");
  RAFT_EXPECTS(codes.extent(1) == get_quantized_dim(quant.params_quantizer),
               "Codes matrix doesn't have the correct number of columns");
  RAFT_EXPECTS(quant.params_quantizer.pq_bits >= 4 && quant.params_quantizer.pq_bits <= 16,
               "PQ bits must be within [4, 16]");
  reconstruct_vectors<T, T, idx_t, label_t>(
    res,
    quant.params_quantizer,
    codes,
    raft::make_const_mdspan(quant.vpq_codebooks.pq_code_book.view()),
    raft::make_const_mdspan(quant.vpq_codebooks.vq_code_book.view()),
    vq_labels,
    out,
    quant.params_quantizer.use_subspaces);
}

template <typename NewMathT, typename OldMathT, typename IdxT>
void vpq_convert_math_type(const raft::resources& res,
                           const cuvs::neighbors::vpq_dataset<OldMathT, IdxT>& src,
                           cuvs::neighbors::vpq_dataset<NewMathT, IdxT>& dst)
{
  raft::linalg::map(res,
                    dst.vq_code_book.view(),
                    cuvs::spatial::knn::detail::utils::mapping<NewMathT>{},
                    raft::make_const_mdspan(src.vq_code_book.view()));
  raft::linalg::map(res,
                    dst.pq_code_book.view(),
                    cuvs::spatial::knn::detail::utils::mapping<NewMathT>{},
                    raft::make_const_mdspan(src.pq_code_book.view()));
}

template <typename DatasetT, typename MathT, typename IdxT>
auto vpq_build(const raft::resources& res,
               const cuvs::neighbors::vpq_params& params,
               const DatasetT& dataset) -> cuvs::neighbors::vpq_dataset<MathT, IdxT>
{
  using label_t = uint32_t;
  // Use a heuristic to impute missing parameters.
  auto ps = cuvs::neighbors::detail::fill_missing_params_heuristics(params, dataset);

  // Train codes
  auto vq_code_book = cuvs::neighbors::detail::train_vq<MathT>(res, ps, dataset);
  auto pq_code_book = cuvs::neighbors::detail::train_pq<MathT>(
    res, ps, dataset, raft::make_const_mdspan(vq_code_book.view()));

  // Encode dataset
  const IdxT n_rows       = dataset.extent(0);
  const IdxT codes_rowlen = sizeof(label_t) * (1 + raft::div_rounding_up_safe<IdxT>(
                                                     ps.pq_dim * ps.pq_bits, 8 * sizeof(label_t)));

  auto codes = raft::make_device_matrix<uint8_t, IdxT, raft::row_major>(res, n_rows, codes_rowlen);
  cuvs::neighbors::detail::process_and_fill_codes<MathT, IdxT>(
    res,
    ps,
    dataset,
    raft::make_const_mdspan(pq_code_book.view()),
    raft::make_const_mdspan(vq_code_book.view()),
    raft::make_device_vector_view<label_t, IdxT>(nullptr, 0),
    codes.view(),
    true);

  return cuvs::neighbors::vpq_dataset<MathT, IdxT>{
    std::move(vq_code_book), std::move(pq_code_book), std::move(codes)};
}

template <typename DatasetT>
auto vpq_build_half(const raft::resources& res,
                    const cuvs::neighbors::vpq_params& params,
                    const DatasetT& dataset) -> cuvs::neighbors::vpq_dataset<half, int64_t>
{
  auto old_type = vpq_build<decltype(dataset), float, int64_t>(res, params, dataset);
  auto new_type = cuvs::neighbors::vpq_dataset<half, int64_t>{
    raft::make_device_mdarray<half>(res, old_type.vq_code_book.extents()),
    raft::make_device_mdarray<half>(res, old_type.pq_code_book.extents()),
    std::move(old_type.data)};
  vpq_convert_math_type<half, float, int64_t>(res, old_type, new_type);
  return new_type;
}
}  // namespace cuvs::preprocessing::quantize::pq::detail
